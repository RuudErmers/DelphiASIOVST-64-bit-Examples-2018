unit URMCShape;

interface

uses System.SysUtils, System.Classes, Messages, Vcl.Controls, Vcl.ExtCtrls, Vcl.Graphics,Types, URMCConstants,UKnobEditor;

{ interface.interface }
type TRMCProcChanged = reference to procedure (sender:TObject; newvalue:integer);

  TRMCShape = class
  procedure Paint; virtual;abstract;
  procedure SetValue(aValue:integer);virtual;abstract;
  procedure OnMouseDown(Sender:TObject;  Button: TMouseButton; Shift: TShiftState; X, Y: Integer);virtual;abstract;
  procedure OnMouseUp(Sender: TObject;  Button: TMouseButton; Shift: TShiftState; X, Y: Integer);virtual;abstract;
  procedure OnMouseMove(Sender: TObject; Shift: TShiftState; X, Y: Integer);virtual;abstract;
  procedure OnMouseClick(Sender: TObject; Button: TMouseButton; Shift: TShiftState; X, Y: Integer);virtual;
  procedure SetAttributeValue(msg,value: integer);virtual;abstract;
  procedure DefaultWH(VAR w,h:integer);virtual;abstract;

end;

function CreateShape(owner:TComponent;shape:TRMCKnobShape;procChanged:TRMCProcChanged):TRMCShape;

implementation

{ implementation.interface }

uses URMCControls, Windows,Math,Dialogs,URMCBitmaps,URMC7Segment,Vcl.Imaging.pngimage; //,URMCBitSlicer;
type   TVCLBitmap = Vcl.Graphics.TBitmap;
       ArrayOfInteger = TArray<integer>;
TRMCShapeBaseImp = class (TRMCShape)
  private
      FThumbStartP:TPoint;
      FThumbStartValue:integer;
      FThumping,FMouseHasMoved:boolean;
      FProcChanged:TRMCProcChanged;
{ property} shape:TRMCKnobShape;
{ property} value:integer;
      RMCElement:TRMCElement;
      procedure Init;virtual;
      procedure SetValue(aValue:integer);override;
      function MouseMoveUseX:boolean; virtual;
      procedure DefaultWH(VAR w,h:integer);override;
      function MouseRange:integer;virtual;
      function MouseInvert:boolean;virtual;
//      procedure Paint;virtual;abstract;
      procedure OnMouseDown(Sender:TObject;  Button: TMouseButton; Shift: TShiftState; X, Y: Integer);override;
      procedure OnMouseUp(Sender: TObject;  Button: TMouseButton; Shift: TShiftState; X, Y: Integer);override;
      procedure OnMouseMove(Sender: TObject; Shift: TShiftState; X, Y: Integer);override;
      constructor Create(owner:TComponent;shape:TRMCKnobShape;procChanged:TRMCProcChanged);virtual;
      procedure StartMouseKnob(X, Y, knob: integer);
      function CalcButtonColor: TColor;
      procedure SetAttributeValue(msg,value:integer);override;
      function KnobEditor:TKnobEditor;
      procedure Line(x1, y1, x2, y2: extended);
end;

    TRMCTwinkle = class (TRMCShapeBaseImp)
  private
      FLightColor:TColor;
      FTimer:TTimer;
      FOwnInvalidate:boolean;
      FSpeed,FPaintPosition:integer;
      procedure SetValue(aValue:integer);override;
      procedure Paint;override;
      procedure Init;override;
      function CalcColor:TColor;
      procedure PaintInner;
      procedure TwinkleTimer(Sender: TObject);
      function NextPosition: boolean;
      function PrevPosition: boolean;
  public
      procedure SetAttributeValue(msg, value :integer);override;
    end;

    TRMCSunrise = class (TRMCShapeBaseImp)
  private
      procedure Paint;override;
  public
    end;

    TOnOwnerDraw = procedure (Sender:TRMCElement) of object;
    TRMCOwnerDraw = class (TRMCShapeBaseImp)
  private
      procedure Paint;override;
  public
      OnOwnerDraw:TOnOwnerDraw;
    end;

   TRMCElementRMC = class(TRMCShapeBaseImp)
     function RMCMouseRange(defvalue: integer): integer;
     procedure Paint;override;
  end;
   TRMCElementVA = class(TRMCShapeBaseImp)
     procedure KnobPaint(w,h,mx,my,r1,r2:integer;getBitmap:TVCLBitmap; count:integer;values:ArrayOfInteger);
     procedure Paint;override;
     procedure DefaultWH(VAR w,h:integer);override;
  end;

   TRMCElementRol = class(TRMCShapeBaseImp)

  private
     FSaveValue:integer;
   protected
     procedure RolandKnobPaint;
     procedure RolandVSliderPaint;
     procedure RolandHSliderPaint;
     procedure RolandTextPaint;
     function MouseInvert: boolean;override;
     function MouseRange : integer;override;
     function MouseMoveUseX:boolean; override;
     procedure Paint;override;
      procedure OnMouseClick(Sender: TObject;Button: TMouseButton; Shift: TShiftState; X, Y: Integer);override;

   end;

{ implementation.implementation }

function RMCStyle(shape:TRMCKnobShape): TRMCStyle;
begin
  case shape of
    trKnob,trButton,trSlider,trNone,trMidi4,trLedButton,trTextButton,trText,trLed,trTextLine,trPitchKnob:result:=tsRMC;
    tkVCOWave,tkLFOWave,tkFoot,tkNoise,tkValue,tkNone,tkLFO,tkSlider,tkSliderMulti: result:=tsRoland;
    tvWave,tvFoot,tvKnob,tvLFO, tvLFOSel, tvWaveFoot4, tvWaveShape4,tvLOF1, tvLFO2: result:=tsVASynth;
    trTwinkle: result:=tsTwinkle;
    tsKnob,tsButton,tsSlider,tsLed: result:=tsSunrise;
    toOwnerDraw: result:=tsOwnerDraw;
    else result:=tsError;
  end;
end;

function CreateShape(owner:TComponent;shape:TRMCKnobShape;procChanged:TRMCProcChanged):TRMCShape;
begin
  case RMCStyle(shape) of
    tsRMC:    result:=TRMCElementRMC.Create(owner,shape,procChanged);
    tsRoland: result:=TRMCElementRol.Create(owner,shape,procChanged);
    tsVASynth:result:=TRMCElementVA.Create(owner,shape,procChanged);
    tsTwinkle: result:=TRMCTwinkle.Create(owner,shape,procChanged);
    tsSunrise: result:=TRMCSunrise.Create(owner,shape,procChanged);
    tsOwnerDraw: result:=TRMCOwnerDraw.Create(owner,shape,procChanged);
    tsError:  result:=NIL;
  end;
end;

constructor TRMCShapeBaseImp.Create(owner: TComponent;shape:TRMCKnobShape;procChanged:TRMCProcChanged);
begin
//  inherited Create(NIL);
  RMCElement:=TRMCElement(owner);
  FProcChanged:=ProcChanged;
  self.Shape:=shape;
  FThumping:=false;
  Init;
end;

procedure TRMCShapeBaseImp.DefaultWH(var w, h: integer);
begin

end;

procedure TRMCShapeBaseImp.Init;
begin

end;

function TRMCShapeBaseImp.KnobEditor: TKnobEditor;
begin
  result:=RMCElement.KnobEditor;
end;

procedure TRMCShapeBaseImp.OnMouseUp(Sender: TObject;  Button: TMouseButton; Shift: TShiftState; X, Y: Integer);
begin
  FThumping:=false;
  if not FMouseHasMoved then OnMouseClick(Sender,Button,Shift,X,Y);
  if (Shape = trPitchKnob) and (KnobEditor<>NIL) then
             KnobEditor.EditKnobMouseUp(RMCElement.index);

end;

procedure TRMCShapeBaseImp.OnMouseMove(Sender: TObject; Shift: TShiftState; X, Y: Integer);
VAR newValue,d:integer;
begin
  if FThumping then
  begin
    FMouseHasMoved:=true;
    if MouseMoveUseX then d:=X-FThumbStartP.X // default move is left to right
                     else d:=FThumbStartP.Y-Y;// default move is up

    if MouseInvert then d:=-d;
    if d>MouseRange then d:=MouseRange;
    if d<-MouseRange then d:=-MouseRange;
    newValue:=FThumbStartValue+d * (RMCElement.maxValue - RMCElement.minValue) DIV MouseRange;
    if newValue< RMCElement.minValue then newValue:= RMCElement.minValue;
    if newValue> RMCElement.maxValue then newValue:= RMCElement.maxValue;
    if newValue<>Value then
    begin
      value:=newValue;
      if (Shape = trPitchKnob) and (KnobEditor<>NIL) then
             KnobEditor.EditKnobMouseMove(RMCElement.index,value);
      FProcChanged(self,Value);
    end;
  end;
end;

procedure TRMCShapeBaseImp.SetAttributeValue(msg, value:integer);
begin

end;

procedure TRMCShapeBaseImp.SetValue(aValue: integer);
begin
  Value:=aValue;
end;

procedure TRMCShapeBaseImp.StartMouseKnob(X,Y,knob:integer);
begin
  FThumbStartP:=Point(X,Y);
  FThumbStartValue:=Value;
  FThumping:=true;
  FMouseHasMoved:=false;
end;


function TRMCShapeBaseImp.MouseMoveUseX:boolean;
begin
  result:=false;
end;

function TRMCShapeBaseImp.MouseInvert: boolean;
begin
  result:=false;
end;

function TRMCShapeBaseImp.MouseRange: integer;
begin
  if RMCElement.maxValue - RMCElement.minValue > 5 then
    result:=200
  else
    result:=100;
end;

procedure TRMCShapeBaseImp.OnMouseDown(Sender:TObject;  Button: TMouseButton; Shift: TShiftState; X, Y: Integer);
  function CalcMidi(value:integer):integer;
  VAR midi:integer;
  begin
    midi:=ord(x>RMCElement.Width DIV 2)+ 2*ord(y>RMCElement.Height DIV 2);
    if value and (1 shl midi) <> 0
       then
         result:=value and not (1 shl midi)
       else
         result:=value or (1 shl midi);
  end;
  function CalcLFOSelect(value:integer):integer;
  begin
    value:=value DIV 32;
    result:=round(127*(value xor (1 shl ord(y>RMCElement.Height DIV 2)) )/3);
  end;

begin
  with TRMCElement(Sender) do
  begin
    case Shape of
      trNone  :   ;
      trLedButton,
      trLed,
      trTextButton,
      trTwinkle,
      tsButton,
      tsLed,
      trButton:  begin
                   value:=127-value;
                   FProcChanged(self,Value);
                 end;
      trMidi4 :  begin
                   value:=CalcMidi(value);
                   FProcChanged(self,Value);
                 end;
      trPitchKnob,
      trKnob:     begin
                      StartMouseKnob(X,Y,index);
                      if (Shape = trPitchKnob) and (KnobEditor<>NIL) then
                        KnobEditor.EditKnobMouseDown(index,value);
                  end;

      else        StartMouseKnob(X,Y,index);
    end;
  end;
end;


function TRMCShapeBaseImp.CalcButtonColor:TColor;
begin
  if Value<>0 then result:=RMCElement.ButtonColorOn else result:=RMCElement.ButtonColorOff;
  if (result=clNone) or not RMCElement.Visible then result:=RMCElement.BackColor;
end;

procedure CopyAntiAliased(handlet:THandle; wt,ht:integer;handles:THandle; ws,hs:integer);
begin
  SetStretchBltMode(Handlet, HALFTONE);
  StretchBlt(Handlet, 0, 0, wt,ht,handles,0,0,ws,hs, SRCCOPY);
end;


function dx(r,alpha:integer):integer;
begin
  result:=round(r*cos(pi*alpha / 180));
end;
function dy(r,alpha:integer):integer;
begin
  result:=round(r*sin(pi*alpha / 180));
end;

{ TRMCTwinkle }

procedure TRMCTwinkle.Init;
begin
  inherited;
  FTimer:=TTimer.Create(NIL);
  FTimer.OnTimer:=TwinkleTimer;
  FTimer.Interval:=16;
  FTimer.Enabled:=false;
  FSpeed:=8;
  FLightColor:=$252525;
  FPaintPosition:=0;
end;

procedure TRMCTwinkle.Paint;
VAR cl:TColor;
     r:Trect;
begin
  if not FOwnInvalidate then
  with RMCElement,Canvas do
  begin
    Brush.Color:= BackColor;
    Pen.Color:=Brush.Color;
    Brush.Style:=bsSolid;
    Pen.Style:=psSolid;
    Pen.Width:=1;
    r:=Rect(0,0,Width,Height);
    Rectangle(0,0,Width+1,Height+1);
  end;
  PaintInner;
end;

procedure TRMCTwinkle.PaintInner;
VAR cl:TColor;
     r:Trect;
begin
  with RMCElement,Canvas do
  begin
    cl:=FLightColor;
    Brush.Color:=cl;
    Pen.Color:=clBlack;
    Pen.Style:=psSolid;
    Pen.Width:=2;
    r:=Rect(0,0,Width,Height);
    Ellipse(r.Left,r.Top,r.Right,r.Bottom);
  end;
end;

function RGB(r,g,b:integer):integer;begin result:= b SHL 16 + g SHL 8 + r; end;

function TRMCTwinkle.CalcColor:TColor;
VAR x:double;
    r,g,b,fx:integer;
begin
  x:= FPaintPosition / 255;
  fx:=round(sqrt(x)*255);
  b:=$25 + round((128-$25)*2*x);
  if x<0.5 then result:=RGB(b,b,b) //fx,fx,fx)
           else result:=RGB(fx,fx,fx DIV 3);             // light yellow = rgb(255,255,102)
end;

function TRMCTwinkle.NextPosition:boolean;
VAR newPosition:integer;
begin
  newPosition:=FPaintPosition+FSpeed;
  if newPosition>=256 then
  begin
    result:=false;
    FPaintPosition:=255;
  end
  else begin result:=true; FPaintPosition:=newPosition; end;
  FLightColor:=CalcColor;
end;

function TRMCTwinkle.PrevPosition:boolean;
VAR newPosition:integer;
begin
  newPosition:=FPaintPosition-FSpeed;
  if newPosition<0 then begin result:=false; FPaintPosition:=0; end
                      else begin result:=true; FPaintPosition:=newPosition; end;

  FLightColor:=CalcColor;
end;


procedure TRMCTwinkle.TwinkleTimer(Sender:TObject);
begin
  if Value>0 then FTimer.Enabled:=NextPosition
             else FTimer.Enabled:=PrevPosition;
  FOwnInvalidate:=true;
  RMCElement.Invalidate;
  FOwnInvalidate:=false;
end;

procedure TRMCTwinkle.SetAttributeValue(msg,value:integer);
begin
  if msg = RMCMSG_TWINKLESPEED then
    FSpeed:=value;
end;

procedure TRMCTwinkle.SetValue(aValue: integer);
begin
  inherited;
  FTimer.Enabled:=true;
end;

procedure TRMCShapeBaseImp.Line(x1, y1, x2, y2: extended);
begin
  with RMCElement.Canvas do
  begin
    MoveTo(round(x1),round(y1));
    LineTo(round(x2),round(y2));
  end;
end;

function TRMCElementRol.MouseInvert: boolean;
begin
  result:=(Shape=tkVCOWave) or (Shape = tkLFO) ;
end;

function TRMCElementRMC.RMCMouseRange(defvalue:integer): integer;
begin
  if RMCElement.maxValue - RMCElement.minValue > 5 then
    result:=200
  else
    result:=100;
end;

function TRMCElementRol.MouseRange: integer;
begin
  if MouseMoveUseX then result:=RMCElement.Width
                   else result:=RMCElement.Height;
end;

procedure TRMCElementRMC.Paint;
    procedure DrawKnob;
    VAR r:TRect;
        alpha,r1,r2,xm,ym:integer;
        bm:TVCLBitmap;
    begin
      with RMCElement,Canvas do
      begin
        r:=Rect(0,0,Width,Height);
        bm:=getBitmap(BmpRMCKnob);
        bm.transparent:=true;
        StretchDraw(r,bm);
        alpha:=240-300*(Value-minValue) DIV (maxValue-minValue);
        r1:=round(15*Width DIV bm.Width) ;
        r2:=round(24*Width DIV bm.Width) ;
        xm:=Width div 2;
        ym:=Height DIV 2;
        MoveTo(xm+dx(r1,alpha),ym-dy(r1,alpha));
        Pen.Color:=clWhite;
        Pen.Width:=2;
        LineTo(xm+dx(r2,alpha),ym-dy(r2,alpha));
      end;
    end;

    procedure DrawVerSlider;
    VAR xm,yt,yb,yv,ym,i:integer;
    begin
      with RMCElement,Canvas do
      begin
        xm:=Width DIV 2;
        yt:=4;
        yb:=Height-4;
        yv:=yb-((yb-yt)*(Value-minValue)) DIV (maxValue-minValue);
        Brush.Color:=BackColor;
        Brush.Style:=bsSolid;
        Pen.Style:=psClear;
        Rectangle(0,0,Width+1,Height+1);
        Pen.Color:=RGB(156,145,148);
        Pen.Width:=1;
        Pen.Style:=psSolid;
        for i:=0 to 10 do
        begin
          MoveTo(xm,yt+ i*(yb-yt) DIV 10); LineTo(xm+5,yt+i*(yb-yt) DIV 10);
        end;
        Pen.Color:=clWhite;
        Pen.Width:=1;
        MoveTo(xm,yt); LineTo(xm,yb);
        MoveTo(xm,yt); LineTo(xm+10,yt);
        MoveTo(xm,yb); LineTo(xm+10,yb);
        MoveTo(xm,(yb+yt) DIV 2); LineTo(xm+5,(yb+yt) DIV 2);
        Pen.Color:=sliderColor;
        Pen.Width:=3;
        MoveTo(xm-10,yv); LineTo(xm+10,yv);
      end;
    end;
    procedure DrawHorSlider;
    VAR ym,xt,xb,xv,xm,i:integer;
    begin
      with RMCElement,Canvas do
      begin
        ym:=Height DIV 2;
        xt:=4;
        xb:=Width-4;
        xv:=xb-((xb-xt)*(Value-minValue)) DIV (maxValue-minValue);
        Brush.Color:=BackColor;
        Brush.Style:=bsSolid;
        Pen.Style:=psClear;
        Rectangle(0,0,Width+1,Height+1);
        Pen.Color:=RGB(156,145,148);
        Pen.Width:=1;
        Pen.Style:=psSolid;
        for i:=0 to 10 do
        begin
          MoveTo(ym,xt+ i*(xb-xt) DIV 10); LineTo(ym+5,xt+i*(xb-xt) DIV 10);
        end;
        Pen.Color:=clWhite;
        Pen.Width:=1;
        MoveTo(ym,xt); LineTo(ym,xb);
        MoveTo(ym,xt); LineTo(ym+10,xt);
        MoveTo(ym,xb); LineTo(ym+10,xb);
        MoveTo(ym,(xb+xt) DIV 2); LineTo(ym+5,(xb+xt) DIV 2);
        Pen.Color:=sliderColor;
        Pen.Width:=3;
        MoveTo(ym-10,xv); LineTo(ym+10,xv);
      end;
    end;

    procedure DrawSlider;
    begin
      if RMCElement.Width<RMCElement.Height then DrawVerSlider
                                            else DrawHorSlider;
    end;
    procedure DrawMidi4;
    VAR w,h,i,l,t:integer;
    begin
      with RMCElement,Canvas do
      begin
        Brush.Color:=BackColor;
        Brush.Style:=bsSolid;
        Pen.Style:=psClear;
        Rectangle(0,0,Width+1,Height+1);

        Brush.Color:=clBlue;
        Brush.Style:=bsSolid;
        Pen.Style:=psSolid;
        Pen.Color:=cLgray;
        w:=Width;  // Leeg|  |Leeg| |Leeg   Leeg =4
        h:=Height;
        w:=(w - 4) DIV 2;
        h:=(h - 4) DIV 2;
        for i:=0 to 3 do
        begin
          l:=(i MOD 2)*(w+4);
          t:=(i DIV 2)*(h+4);
          if Value AND  (1 SHL i) <> 0 then Brush.color:=clGray else Brush.Color:=BackColor;
          Rectangle(l,t,l+w,t+h);
        end;
      end;
    end;
    procedure DrawButtonText;
    VAR cl:TColor;
        c:TSize;
        r:Trect;
    begin
      if not RMCElement.visible then exit;
      with RMCElement,Canvas do
      begin
        cl:=CalcButtonColor;
        Brush.Color:=cl;
        Brush.Style:=bsSolid;
        Pen.Color:=ButtonColorOn;
        Pen.Style:=psSolid;
        Pen.Width:=1;
        r:=Rect(0,0,Width,Height);
        FillRect( r );
        Rectangle(r);
        if Value<>0 then Font.Color:=clBlack else Font.Color:=ButtonColorOn;
        if FontSize = 0 then Font.Size:=6
                        else Font.Size:=FontSize;
        inc(r.left);inc(r.top);dec(r.right);dec(r.bottom);
        c:=TextExtent(Text);
        TextRect(r,((r.Left+r.Right) DIV 2)-c.cx DIV 2,(r.Bottom+r.Top) DIV 2-c.cy DIV 2,Text);
      end;
    end;
    procedure DrawLed;
    VAR cl:TColor;
        r:Trect;
    begin
      with RMCElement,Canvas do
      begin
        cl:=CalcButtonColor;
        Brush.Color:=cl;
        Brush.Style:=bsSolid;
        Pen.Color:=cl;
        r:=Rect(0,0,width,height);
        Rectangle(r);
      end;
    end;

    procedure DrawText(lineText:boolean);
    VAR c:TSize;
        cl:TColor;
        r:TRect;
        doline:boolean;
        s,s1,s2:string;
        p,x,y:integer;
    begin
    //  if not visible then exit;

      with RMCElement,RMCElement.Canvas do
      begin
        cl:=RMCElement.Font.Color;
        if cl=clNone then exit;
        Brush.Color:= BackColor;
        Pen.Color:=Brush.Color;
        Brush.Style:=bsSolid;
        Pen.Style:=psSolid;
        Pen.Width:=1;
        r:=Rect(0,0,Width,Height);
        Rectangle(r);

        Pen.Color:=RGB(156,145,148);
        Pen.Style:=psSolid;
        Brush.Style:=bsClear;
        Font.Color:=cl;
        if lineText then
        begin
          Font.Size:=10;
          font.Name:='FixedSys';
          s:=Text;
          if s='' then exit;
          doline:=s[1]='-';
          if doline then s:=Copy(s,2);
          c:=TextExtent(s);
          x:=(r.Left+r.Right) DIV 2-c.cx DIV 2;
          y:=(r.Bottom+r.Top) DIV 2-c.cy DIV 2;
          TextRect(r,x,y,s);
          if doline then
          begin
            y:=y+c.cy DIV 2;
            Pen.Color:=cl;
            Line(0,y,x-4,y);
            Line(0,y+1,x-4,y+1);
            Line(x+4+c.cx,y,Width,y);
            Line(x+4+c.cx,y+1,Width,y+1);
          end;

        end
        else
        begin
          if FontSize = 0 then Font.Size:=6
                          else Font.Size:=FontSize;
          font.Name:='Arial';
          font.Color:=RMCElement.Font.Color;

          c:=TextExtent(Text);
          p:=pos(' ',Text);
          x:=(r.Left+r.Right-c.cx) DIV 2;
          y:=(r.Bottom-2)-c.cy;
          if (x<0) and (p>0) then
          begin
            // retry on two lines...
            s1:=Copy(Text,1,p-1);
            s2:=Copy(Text,p+1);
            c:=TextExtent(s1);
            x:=(r.Left+r.Right) DIV 2-c.cx DIV 2;
            y:=0;
            TextRect(r,x,y,s1);
            c:=TextExtent(s2);
            x:=(r.Left+r.Right) DIV 2-c.cx DIV 2;
            y:=c.cy;
            TextRect(r,x,y,s2);
          end
          else
            TextRect(r,x,y,Text);
        end;
      end;
    end;
    procedure DrawButton;
    VAR cl:TColor;
        r:TRect;
    begin
      with RMCElement,Canvas do
      begin
        Brush.Color:=BackColor; //;
        Brush.Style:=bsSolid;
        Pen.Style:=psClear;
        Rectangle(0,0,Width+1,Height+1);

        Brush.Color:=clBlack;
        Brush.Style:=bsSolid;
        Pen.Style:=psSolid;
        Pen.Color:=clGray;
        RoundRect(0,0,Width,Height,5,5);

        if shape = trLedButton then
        begin
          with Canvas do
            begin
              cl:=CalcButtonColor;
              Brush.Color:=cl;
              Brush.Style:=bsSolid;
              Pen.Color:=cl;
              r:=Rect(Width DIV 2 - 6+Width MOD 2,2*Height DIV 3 - 2,Width DIV 2 + 6,2 *Height DIV 3 + 2);
              Rectangle(r);
            end;

        end;

      end;
    end;
begin
  case Shape of
    trPitchKnob,
    trKnob:         DrawKnob;
    trSlider:       DrawSlider;
    trLed:          DrawLed;
    trText:         DrawText(false);
    trTextLine:     DrawText(true);
    trLedButton,
    trButton:       DrawButton;
    trMidi4:        DrawMidi4;
    trTextButton:   DrawButtonText;
  end;
end;


function TRMCElementRol.MouseMoveUseX: boolean;
begin
  result:= ((shape = tkSlider) or (shape = tkSliderMulti) ) and (RMCElement.Width>RMCElement.Height);
end;

procedure TRMCElementRol.Paint;
begin
  if RMCElement.TextWithSeg7 then RolandTextPaint
  else
  case Shape of
    tkVCOWave,tkLFOWave,tkFoot,tkNoise,tkValue,tkNone:
       RolandKnobPaint;
    tkLFO:
       RolandVSliderPaint;
    tkSlider,tkSliderMulti:
       if RMCElement.Width<RMCElement.Height then
         RolandVSliderPaint
       else
         RolandHSliderPaint;
  end;
end;

{ TRMCElementVA }

procedure TRMCElementVA.DefaultWH(var w, h: integer);
begin
  w:=79;
  h:=52;
  case Shape of
      tvFoot: begin w:=67; h:=60; end;
      tvKnob: begin w:=66; h:=57; end;
  end;
end;

procedure TRMCElementVA.Paint;
VAR bdefault:TVCLBitmap;
begin
  bdefault:=NIL;
  case Shape of
      tvWave: KnobPaint(79,52,37,28,8,20,getBitmap(BmpRMCVAWave),8,ArrayOfInteger.Create(225,195,165,135,45,15,-15,-45));
      tvFoot: KnobPaint(67,60,33,34,8,20,getBitmap(BmpRMCVAFoot),7,ArrayOfInteger.Create(225,180,135,90,45,0,-45));
      tvKnob: KnobPaint(66,57,34,34,13,16,getBitmap(BmpRMCVAKnob),0,NIL);
      tvLFO : KnobPaint(79,52,37,28,8,20,getBitmap(BmpRMCVALFO),4,ArrayOfInteger.Create(225,135,45,-45));
    tvLFOSel: bdefault:=getBitmap(BmpRMCVALFOSEL);
    tvWaveFoot4: bdefault:=getBitmap(BmpRMCVAFoot4);
tvWaveShape4: bdefault:=getBitmap(BmpRMCVAWAVE4);
      tvLOF1: bdefault:=getBitmap(BmpRMCVALFO1);
      tvLFO2: bdefault:=getBitmap(BmpRMCVALFO2);
  end;
  if bdefault<>NIL then
              KnobPaint(79,52,37,28,8,20,bdefault,4,ArrayOfInteger.Create(270-36,270-2*36,270-3*36,270-4*36));
end;

procedure TRMCElementVA.KnobPaint(w,h,mx,my,r1,r2:integer;getBitmap:TVCLBitmap; count:integer;values:ArrayOfInteger);
    function calcAngle(value:integer):integer;
    begin
      if count = 0 then
        result:=225 - round(value*270/127)
      else
        result:=values[trunc(value * count / 128)];
    end;
VAR x1,x2,y1,y2,alpha:integer;
VAR bmp2:TVCLBitmap;
begin
  bmp2:=TVCLBitmap.Create;
  bmp2.SetSize(W,H);
  with bmp2.Canvas do
  begin
    StretchDraw(Rect(0,0,W,H),getBitmap);
    alpha:=calcAngle(value);
    x1:=dx(r1,alpha);
    x2:=dx(r2,alpha);
    y1:=-dy(r1,alpha);
    y2:=-dy(r2,alpha);
    if Shape = tvKnob then
    begin
      Pen.Color:=clBlack;
      Pen.Width:=2;
      MoveTo(mx,my);
      LineTo(mx+x1,my+y1);
      Pen.Color:=clWhite;
      LineTo(mx+x2,my+y2);
    end
    else
    begin
      Pen.Color:=clWhite;
      Pen.Width:=2;
      MoveTo(mx+x1,my+y1);
      LineTo(mx+x2,my+y2);
    end;
  end;
  with RMCElement do
    CopyAntiAliased(Canvas.Handle, Width, Height, Bmp2.Canvas.Handle,Bmp2.Width, Bmp2.Height);
  bmp2.Free;
end;

procedure TRMCElementRol.RolandKnobPaint;
    function dx(r,alpha:integer):integer;
    begin
      result:=round(r*cos(pi*alpha / 180));
    end;
    function dy(r,alpha:integer):integer;
    begin
      result:=round(r*sin(pi*alpha / 180));
    end;
    procedure DrawKnob;
    VAR
        WaveCount,mx,my,grad,positions,i,vvalue,d,dmx,r1,r2,x1,x2,y1,y2:integer;
    begin
      with RMCElement,Canvas do
      begin

        getBitmap(BmpRolandKnob).transparent:=true;
        StretchDraw(Rect(12,13,12+66,10+66),getBitmap(BmpRolandKnob));
        mx:=44;
        my:=13+33;
        Pen.Color:=clLightGray;
        Pen.Width:=1;
        if (Shape = tkNoise) then positions:=3 else positions:=4;
        if (Shape = tkFoot) then positions:=7;
        if (Shape = tkValue) then positions:=10;
        r1:=38;
        d:=1;
        if Shape = tkValue then
          grad:=(270-45)-((360-2*45)*value) DIV (maxvalue+1)
        else if Shape = tkFoot  then
            grad:=195-35*(positions*value DIV (maxvalue+1))  // tussen 195 en -15 ??
        else if (Shape = tkVCOWave) or (Shape = tkLFOWave) then
        begin
          if (Shape = tkVCOWave) then WaveCount:=8 else WaveCount:=4;
          vvalue:=value*WaveCount DIV positions;
          if vvalue>=127 then vvalue:=127;
          grad:=45-30*(positions*vvalue DIV (maxvalue+1));
        end
        else
          grad:=45-30*(positions*value DIV (maxvalue+1));

        dec(mx,1);
        r1:=5;
        r2:=28;
        x1:=dx(r1,grad);
        x2:=dx(r2,grad);
        y1:=-dy(r1,grad);
        y2:=-dy(r2,grad);
        Pen.Color:=clBlack;
        Pen.Width:=3;
        Line(mx+d*x1,my+y1,mx+d*(x2-1),my+y2-1);
        Line(mx+d*x1,my+y1,mx+d*x2,my+y2);
      end;
    end;
    procedure DrawPanel(x1:integer);
    VAR i,y1,dumi:integer;
    begin
      with RMCElement,Canvas do
      begin
        Pen.Width:=1;
        Pen.Color:=clLightGray;
        Pen.Style:=psSolid;

        y1:=92-36-44;
        font.Name:='FixedSys';
        font.size:=8;
        Brush.Style:=bsClear;
        font.Color:=clLightGray;
      end;
    end;
begin
  DrawKnob;
//  if Shape <> tkFoot then
     DrawPanel(87);
end;

procedure TRMCElementRol.RolandVSliderPaint;
VAR bmb,bmu,bms:TVCLBitmap;
    h,hu,huCount,hs,h2,hb,i,t,w2,y:integer;
    fy:single;
    r:TRect;
begin
  with RMCElement,Canvas do
  begin
    bmb:=getBitmap(BmpSliderVerBot);
    bmu:=getBitmap(BmpSliderVerUpp);
    bms:=getBitmap(BmpRolandVSlider);
    w2:=round(width * 0.7);
    hs:=round(bms.height * w2 / bms.width);
    h:=Height-hs;
    if Height>=100 then h:=h-35;
    hb:=round(bmb.height * width / bmb.width);
    hu:=round(bmu.height * width / bmu.width);
    huCount:= round((h-hb) / hu);
    h2:=huCount * hu + hb;
    fy:=h / h2;
    t:=hs DIV 2;
    for i:=0 to huCount-1 do
    // draw scaled bmu at 0,t+i*hu,width,t+(i+1)*hu-1
    begin
      r:=rect(0,round(t+i*fy*hu),width,round(t+(i+1)*fy*hu));
      StretchDraw(r,bmu);
    end;
    // draw scaled getBitmap at t+huCount*hu-1,width, t+huCount*hu-1 + hb - 1
    r:=Rect(0,round(t+fy*huCount*hu),width, round(t+fy*(huCount*hu + hb - 1)));
    StretchDraw(r,bmb);
    y:=hs DIV 2 + round((127-value) * h / 127);
    r:=Rect((width-w2) DIV 2,y-hs DIV 2,(width+w2) DIV 2, y+hs DIV 2);
    bms.transparent:=true;
    StretchDraw(r,bms);
    if Height>=100 then Draw7Segment(Canvas,0,Height-35,value,true,false);
  end;
end;

procedure TRMCElementRol.RolandHSliderPaint;
VAR bmb,bmu,bms:TVCLBitmap;
    w,wu,wuCount,ws,w2,x,wb,i,l,h2:integer;
    fx:single;
    r:TRect;
begin
  with RMCElement,Canvas do
  begin
    bmb:=getBitmap(BmpSliderHorBot);
    bmu:=getBitmap(BmpSliderHorUpp);
    bms:=getBitmap(BmpRolandHSlider);
    h2:=round(height * 0.7);
    ws:=round(bms.width * h2 / bms.height);
    w:=Width-ws;
    wb:=round(bmb.width * height / bmb.height);
    wu:=round(bmu.width * height / bmu.height);
    wuCount:= round((w-wb) / wu);
    w2:=wuCount * wu + wb;
    fx:=w / w2;
    l:=ws DIV 2;
    for i:=0 to wuCount-1 do
    // draw scaled bmu at 0,t+i*hu,width,t+(i+1)*hu-1
    begin
      r:=rect(round(l+i*fx*wu),0,round(l+(i+1)*fx*wu),height);
      StretchDraw(r,bmu);
    end;
    // draw scaled getBitmap at t+huCount*hu-1,width, t+huCount*hu-1 + hb - 1
    r:=Rect(round(l+fx*wuCount*wu),0,round(l+fx*(wuCount*wu + wb - 1)),height);
    StretchDraw(r,bmb);
    x:=ws DIV 2 + round(value * w / 127);
    r:=Rect(x-ws DIV 2,(height-h2) DIV 2,x+ws DIV 2,(height+h2) DIV 2);
    bms.transparent:=true;
    StretchDraw(r,bms);
  end;
end;


procedure TRMCElementRol.OnMouseClick(Sender: TObject; Button: TMouseButton;Shift: TShiftState; X,  Y: Integer);
begin
  with RMCElement do
  begin
    if (shape = tkSlider) or (shape = tkSliderMulti) then
    begin
      if Width<Height then
      begin
        if Y<Height-40 then
        begin
          value :=127- Y * 127 DIV (height-40);
          FProcChanged(self,Value);
        end
        else
        begin
          if value = 0 then
          begin
            value:=FSaveValue;
            FProcChanged(self,Value);
          end
          else
          begin
            FSaveValue:=value;
            value:=0;
            FProcChanged(self,Value);
          end;
        end
      end
      else
        begin
          value :=X * 127 DIV width;
          FProcChanged(self,Value);
        end
    end;
  end;
end;


procedure TRMCElementRol.RolandTextPaint;
VAR c:TSize;
    cl:TColor;
    r:TRect;
    s:string;
    i,v,x,y,WaveCount,dumi:integer;
    const noise : array [ 0..2] of string = ('PNK','BLU','WHT');
    const lfosel : array [ 0..3] of string = ('None','LFO1','LFO2','L1+2');

begin
  with RMCElement do
    case Shape of
      tkNone:  Draw7Segment(Canvas,0,0,Value,true,false);
      tkValue:
        Draw7SegmentEx(Canvas,Rect(0,0,Width,Height),Text,value,deValue);
      tkFoot:
        begin
          v:= 7*value DIV (maxvalue+1);
          Draw7SegmentEx(Canvas,Rect(0,0,Width,Height),Text,v,deNegValue);
        end;
      tkVCOWave,
      tkLFOWave:
        begin
          if Shape = tkVCOWave then WaveCount:=8 else WaveCount:=4;
          v:=WaveCount * value DIV (maxvalue+1);
          if (Shape = tkLFOWave) and (v=3) then v:=7; // noise
          Draw7SegmentEx(Canvas,Rect(0,0,Width,Height),Text,v,deWaveValue);
        end;
      tkNoise:
        begin
          s:=noise[3 * value DIV (maxvalue+1)];
          Draw7SegmentEx(Canvas,Rect(0,0,Width,Height),Text,v,deTextValue,s);
        end;
        tkLFO:
        begin
          s:=lfosel[4 * value DIV (maxvalue+1)];
          Draw7SegmentEx(Canvas,Rect(0,0,Width,Height),Text,v,deTextValue,s);
        end;
    end;
end;

{ TRMCShape }

procedure TRMCShape.OnMouseClick(Sender: TObject; Button: TMouseButton;  Shift: TShiftState; X, Y: Integer);
begin
end;

{ TRMCOverlay }

procedure ColorToRGB(const Color: Integer; out R, G, B: integer);
	begin
	  R := Color and $FF;
	  G := (Color shr 8) and $FF;
	  B := (Color shr 16) and $FF;
	end;
function RGBToColor(const R, G, B: integer): Integer;
begin
  Result := R or (G shl 8) or (B shl 16);
end;

function MixColors(c1,c2:TColor;f:single):TColor;
VAR r,g,b,r1,g1,b1,r2,g2,b2:integer;
begin
  ColorToRGB(c1,r1,g1,b1);
  ColorToRGB(c2,r2,g2,b2);
  r:=round(r1 * (1-f) + r2 * f);
  g:=round(g1 * (1-f) + g2 * f);
  b:=round(b1 * (1-f) + b2 *  f);
  result:=RGBToColor(r,g,b);
end;


{ TRMCSunrise }

procedure TRMCSunrise.Paint;
VAR bm:TVCLBitmap;
    x,y,w,h,t:integer;
begin
  case shape of
    tsButton: with RMCElement,Canvas do
              begin
                if value=0 then bm:=getBitmap(BmpSunriseButOff) else bm:=getBitmap(BmpSunriseButOn);
                if SliderColor=clBlack then bm:=getBitmap(BmpSunriseButFull);
                StretchDraw(Rect(0,0,Width,Height),bm);
              end;
    tsLed:   with RMCElement,Canvas do
              begin
                if value=0 then bm:=getBitmap(BmpSunriseLedOff) else bm:=getBitmap(BmpSunriseLedOn);
                StretchDraw(Rect(0,0,Width,Height),bm);
              end;
    tsKnob:with RMCElement,Canvas do
              begin
                bm:=getBitmap(BmpSunriseKnob);
                StretchDraw(Rect(0,0,Width,Height),bm);
                StretchDraw(Rect(0,0,Width-2,Height),getRotKnob(value));
              end;
    tsSlider:with RMCElement,Canvas do
              begin
                bm:=getBitmap(BmpSunriseSlider);
                StretchDraw(Rect(0,0,Width,Height),bm);
                bm:=getBitmap(BmpSunriseSliderKnob);
                t:= round(Height / 10);
                h:= round(height * 0.8);
                y:=t + round((127-value)*h / 127);
                w:=round(2*width / 3);
                x:=(Width - w) DIV 2;
                h:=round(bm.Height * w / bm.Width);
                y:=y - h DIV 2;
                StretchDraw(Rect(x,y,x+w,y+h),bm);
              end;

  end;
end;

{ TRMCArp }

procedure TRMCOwnerDraw.Paint;
begin
   if assigned(RMCElement.OnSomething) then RMCElement.OnSomething(RMCElement);
end;

end.
